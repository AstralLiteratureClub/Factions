plugins {
    id 'java'

    id 'maven-publish'

    id("io.github.goooler.shadow") version "8.1.3"

    id 'io.freefair.lombok' version '5.1.0'

    id("io.papermc.paperweight.userdev") version "1.5.10"

    id("xyz.jpenilla.run-paper") version "2.2.2"
}

group = 'bet.astral'
version = '1.0'

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }

    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
        name "sonatype-snapshots"
        mavenContent {
            snapshotsOnly()
        }
    }
}

dependencies {
    implementation 'bet.astral:Messenger:1.5.1' // Messenger

    // Cloud and brigadier for commands
    compileOnly 'com.mojang:brigadier:1.0.18'
    var cloudVersion = "2.0.0-SNAPSHOT";
    implementation 'org.incendo:cloud-core:'+cloudVersion
    implementation 'org.incendo:cloud-paper:'+cloudVersion
    implementation 'org.incendo:cloud-brigadier:'+cloudVersion
    implementation 'org.incendo:cloud-minecraft-extras:'+cloudVersion

    implementation 'org.projectlombok:lombok:1.18.26'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'

    implementation 'org.apache.commons:commons-math3:3.6.1'
    // Classgraph for reflections of commands
    implementation 'io.github.classgraph:classgraph:4.8.165'

    compileOnly "io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT"
    paperweight.paperDevBundle("1.20.4-R0.1-SNAPSHOT")
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}

shadowJar {
    archiveClassifier = null

    relocate 'bet.astral.messenger', 'bet.astral.unity.libs.bet.astral.messenger'

    relocate 'io.github.classgraph', "bet.astral.unity.libs.io.github.classgraph"
    relocate 'org.incendo.cloud', 'bet.astral.unity.libs.org.incendo.cloud'
    relocate 'org.apache.commons', 'bet.astral.unity.libs.org.apache.commons'
}

tasks {
    runServer {
        minecraftVersion("1.20.4")
    }
}