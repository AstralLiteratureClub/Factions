plugins {
    id 'java'

    id 'maven-publish'

    id("io.github.goooler.shadow") version "8.1.3"

    id 'io.freefair.lombok' version '5.1.0'

    id("io.papermc.paperweight.userdev") version "1.5.10"

    id("xyz.jpenilla.run-paper") version "2.2.2"
}

group = 'bet.astral'
version = '1.0'

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }

    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
        name "sonatype-snapshots"
        mavenContent {
            snapshotsOnly()
        }
    }

    // PAPI
    maven {
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
}

dependencies {
    implementation 'bet.astral:Messenger:1.6.0' // Messenger

    // Cloud and brigadier for commands
    compileOnly 'com.mojang:brigadier:1.0.18'
    var cloudVersion = "2.0.0-SNAPSHOT";
    compileOnly 'org.incendo:cloud-core:' + cloudVersion
    compileOnly 'org.incendo:cloud-paper:' + cloudVersion
    compileOnly 'org.incendo:cloud-brigadier:' + cloudVersion
    compileOnly 'org.incendo:cloud-minecraft-extras:' + cloudVersion

    // SLF4j
    compileOnly('org.slf4j:slf4j-simple:2.0.9')

    /*
     * Databases
     */
    // Hikari thread pool for sql based databases
    compileOnly ('com.zaxxer:HikariCP:5.1.0')


    // Lombok
    implementation 'org.projectlombok:lombok:1.18.26'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'

    // Classgraph for reflections of commands
    compileOnly 'io.github.classgraph:classgraph:4.8.165'

    compileOnly 'me.clip:placeholderapi:2.11.5'

    compileOnly "io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT"
    paperweight.paperDevBundle("1.20.4-R0.1-SNAPSHOT")
}


// Some shit is importing 1.13 bukkit api and not 1.20.1 api
configurations.compileOnly {
    exclude group: 'org.spigotmc', module: 'spigot'
    exclude group: 'org.spigotmc', module: 'spigot-api'
    exclude group: 'org.bukkit', module: 'bukkit'
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}

tasks.assemble {
    dependsOn(reobfJar)
}

jar {
    enabled = false
    dependsOn(shadowJar)
}

shadowJar {
    archiveClassifier = "main"

    relocate 'bet.astral.messenger', 'bet.astral.unity.libs.bet.astral.messenger'
}


publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
        }
    }
}

tasks {
    runServer {
        minecraftVersion("1.20.4")
    }

}